buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'maven'
    id 'application'

    id 'com.google.protobuf' version '0.8.11'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.google.cloud.tools.jib' version '2.0.0'
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}


group = 'io.emeraldpay.polkadotcrawler'
version = '0.3-SNAPSHOT'

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url  "https://dl.bintray.com/emerald/noise-java" }
}

configurations {
    compile.exclude group: "commons-logging"
    compile.exclude group: "ch.qos.logback"
    compile.exclude group: "org.slf4j", module: "slf4j-jdk14"
    compile.exclude group: "org.slf4j", module: "log4j-over-slf4j"
}

dependencies {
    compile 'io.emeraldpay.noise:noise:1.0'
    compile 'com.google.protobuf:protobuf-java:3.11.4'
    compile 'io.libp2p:jvm-libp2p-minimal:0.3.1-RELEASE'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    compile "org.springframework:spring-core:$springVersion"
    compile "org.springframework:spring-context:$springVersion"
    compile "org.springframework.boot:spring-boot:$springBootVersion"
    compile("org.springframework.boot:spring-boot-starter:$springBootVersion") {
        exclude module: 'spring-boot-starter-logging'
    }

    compile "io.projectreactor:reactor-core:$reactorVersion"
    compile "io.projectreactor.netty:reactor-netty:$reactorNettyVersion"
    compile "io.projectreactor.addons:reactor-extra:$reactorVersion"

    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'org.apache.commons:commons-collections4:4.3'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'org.bouncycastle:bcprov-jdk15on:1.61'
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.11.1"
    compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"


    testCompile "org.codehaus.groovy:groovy:$groovyVersion"
    testCompile 'cglib:cglib-nodep:3.2.12'
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "io.projectreactor:reactor-test:$reactorVersion"
    testCompile 'org.objenesis:objenesis:3.0.1'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

processResources {
    // setup project version into default properties
    from('src/main/resources') {
        include "application.properties"
        expand projectVersion: project.version
    }

    // copy all other properties, without trying to expand
    from('src/main/resources') {
        exclude "application.properties"
    }
}

application {
    mainClassName = 'io.emeraldpay.polkadotcrawler.StarterKt'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
    }
    generateProtoTasks {
    }
}

afterEvaluate {
    generateProto.dependsOn(clean)
}

jib {
    from {
        // can also be openjdk:13-alpine, but bouncycastle may be not fully compatible with Java 13 yet; there is no 11-alpine
        image = 'openjdk:11-jdk-slim'
    }
    to {
        image = 'emeraldpay/polkabot'
        tags = ['2', 'beta']
    }
    container {
        volumes = ["/results"]
        args = ["--export.file.targetdir=/results"]
    }
}